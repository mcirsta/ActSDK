OUTPUT_ARCH(mips) 

ENTRY(_init)

PHDRS
{
	text PT_LOAD ;
	data PT_LOAD ;
}

SECTIONS
{
	.init :
	{
	 *(.init)
	}:text
	
	.text : 
	{
	 *(.text)
	 *(.rodata)
	 *(.rodata.*)
	}:text

	.fini :
	{
	 *(.fini)
	}:text

	.dlsym	:
	{
		*(.dlsym)
	}:text

	.dlstr	:
	{
		*(.dlstr)
	}:text
   	

   	. = ALIGN (0x1000);	
	.ctors	:
	{
	/* gcc uses crtbegin.o to find the start of
	   the constructors, so we make sure it is
	   first.  Because this is a wildcard, it
	   doesn't matter if the user does not
	   actually link against crtbegin.o; the
	   linker won't look for a file to match a
	   wildcard.  The wildcard also means that it
	   doesn't matter which directory crtbegin.o
	   is in.  */
	KEEP (*crtbegin*.o(.ctors))
	/* We don't want to include the .ctor section from
	   from the crtend.o file until after the sorted ctors.
	   The .ctor section from the crtend file contains the
	   end of ctors marker and it must be last */
	KEEP (*(EXCLUDE_FILE (*crtend*.o ) .ctors))
	KEEP (*(SORT(.ctors.*)))
	KEEP (*(.ctors))
	}:data
	
	.dtors	:
	{
	KEEP (*crtbegin*.o(.dtors))
	KEEP (*(EXCLUDE_FILE (*crtend*.o ) .dtors))
	KEEP (*(SORT(.dtors.*)))
	KEEP (*(.dtors))
	}:data

	.data :
	{
	 *(.data)
	 *(.sdata)
	}:data
	
	.sbss : 
  	{ 	
  	 *(.sbss) 
 	 *(.scommon)
 	}:data
 	
	.bss :
 	{
 	 *(.bss)
  	 *(COMMON)
 	 }:data
 	 
 } 